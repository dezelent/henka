/*
* Copyright (c) 2016, Henka Contributors
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with
* the License. You may obtain a copy of the License at
*
* http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing,
* software distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and limitations under the License.
*/
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'maven'

group = 'com.roku'
archivesBaseName = 'henka'
version = '1.0.0-SNAPSHOT'
project.ext.artifactoryHost = 'artifactory.cs.roku.com:8081'


project.ext.'signing.keyId' = System.env['ORG_GRADLE_PROJECT_signing_keyId']
project.ext.'signing.password' = System.env['ORG_GRADLE_PROJECT_signing_password']
project.ext.'signing.secretKeyRingFile' = System.env['ORG_GRADLE_PROJECT_signing_secretKeyRingFile']

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'org.apache.commons:commons-lang3:3.4'
    compile localGroovy()
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Henka'
        packaging 'jar'
        artifactId = 'henka'
        description 'Opionated Gradle Plugin for Terraform and AWS'
        url 'https://github.com/rokudev/henka/'

        scm {
          connection 'scm:git:git@github.com:rokudev/henka.git'
          developerConnection 'scm:git:git@github.com:rokudev/henka.git'
          url 'https://github.com/rokudev/henka/'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'mprotsenko'
            name 'Mykyta Protsenko'
            email 'mprotsenko@roku.com'
          }
        }
      }
    }
  }
}

groovydoc {
}

task groovydocJar(type: Jar) {
    classifier = 'javadoc'
    from groovydoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives groovydocJar, sourcesJar
}

groovydocJar.onlyIf {  System.env['RELEASE_BUILD'] != null }
sourcesJar.onlyIf {  System.env['RELEASE_BUILD'] != null }
signArchives.onlyIf {  System.env['RELEASE_BUILD'] != null }

